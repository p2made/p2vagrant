# 04 Install Apache (with SSL ðŸ™ƒ)

Updated: 2024-02-02

--

In addition to using `fish`, I start putting anything that's used more than once into an include file, `common_functions.fish`. Which brings us to...

### Create `provision/scripts/common_functions.fish `

```
#!/bin/fish

# common_functions.fish
# Last Updated: 2024-02-01

# Script constants...

# TODAYS_DATE         $(date "+%Y-%m-%d")
# VM_FOLDER           /var/www
# SHARED_HTML         $VM_FOLDER/html
# PROVISION_FOLDER    $VM_FOLDER/provision
# PROVISION_DATA      $VM_FOLDER/provision/data
# PROVISION_HTML      $VM_FOLDER/provision/html
# PROVISION_LOGS      $VM_FOLDER/provision/logs
# PROVISION_SCRIPTS   $VM_FOLDER/provision/scripts
# PROVISION_SSL       $VM_FOLDER/provision/ssl
# PROVISION_TEMPLATES $VM_FOLDER/provision/templates
# PROVISION_VHOSTS    $VM_FOLDER/provision/vhosts

set -U TODAYS_DATE         (date "+%Y-%m-%d")
set -U VM_FOLDER           /var/www
set -U SHARED_HTML         $VM_FOLDER/html
set -U PROVISION_FOLDER    $VM_FOLDER/provision
set -U PROVISION_DATA      $VM_FOLDER/provision/data
set -U PROVISION_HTML      $VM_FOLDER/provision/html
set -U PROVISION_LOGS      $VM_FOLDER/provision/logs
set -U PROVISION_SCRIPTS   $VM_FOLDER/provision/scripts
set -U PROVISION_SSL       $VM_FOLDER/provision/ssl
set -U PROVISION_TEMPLATES $VM_FOLDER/provision/templates
set -U PROVISION_VHOSTS    $VM_FOLDER/provision/vhosts

# Function for error handling
# Usage: handle_error "Error message"
function handle_error
	echo "âš ï¸ Error: $argv ðŸ’¥"
	exit 1
end

# Function to announce success
# Usage: announce_success "Task completed successfully." [use_alternate_icon]
function announce_success
	set icon "âœ…"

	if test -n "$argv[2]"
		if test "$argv[2]" -eq 1
			set icon "ðŸ‘"
		end
	end

	echo "$icon $argv[1]"
end

# Function to update package with error handling
# Usage: update_package_lists
function update_package_lists
	echo "ðŸ”„ Updating package lists ðŸ”„"

	if not apt-get -q update > /dev/null 2>&1
		handle_error "Failed to update package lists"
	end

	announce_success "Package lists updated successfully."
end

# Function to install packages with error handling
# Usage: install_packages $package_list
function install_packages
	echo "ðŸ”„ Installing Packages ðŸ”„"

	for package in $argv
		set cleaned (eval echo $package)
		if not apt-get -qy install $cleaned
			handle_error "Failed to install packages"
		end
	end

	announce_success "Packages installed successfully!"
end

# Function to update package lists the install packages with error handling
# invokes update_package_lists & install_packages in a single call
# Usage: update_and_install_packages $package_list
function update_and_install_packages
	update_package_lists
	install_packages $argv
end

# -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- --

function header_banner
	echo "ðŸ‡ºðŸ‡¦ ðŸ‡°ðŸ‡¿ ðŸ‡°ðŸ‡¬ ðŸ‡¹ðŸ‡¯ ðŸ‡¹ðŸ‡² ðŸ‡ºðŸ‡¿ ðŸ‡¦ðŸ‡¿ ðŸ‡²ðŸ‡³ ðŸ‡ºðŸ‡¦ ðŸ‡°ðŸ‡¿ ðŸ‡°ðŸ‡¬ ðŸ‡¹ðŸ‡¯ ðŸ‡¹ðŸ‡² ðŸ‡ºðŸ‡¿ ðŸ‡¦ðŸ‡¿ ðŸ‡²ðŸ‡³ ðŸ‡ºðŸ‡¦"
	echo "ðŸ‡ºðŸ‡¦"
	echo "ðŸ‡ºðŸ‡¦    ðŸš€ $argv[1] ðŸš€"
	echo "ðŸ‡ºðŸ‡¦        on ðŸ“… $TODAYS_DATE ðŸ“…"
	echo "ðŸ‡ºðŸ‡¦"
	echo "ðŸ‡ºðŸ‡¦    ðŸ“œ Script Name:  $argv[2]"
	echo "ðŸ‡ºðŸ‡¦    ðŸ“… Last Updated: $argv[3]"
	echo "ðŸ‡ºðŸ‡¦"
	echo "ðŸ‡ºðŸ‡¦ ðŸ‡°ðŸ‡¿ ðŸ‡°ðŸ‡¬ ðŸ‡¹ðŸ‡¯ ðŸ‡¹ðŸ‡² ðŸ‡ºðŸ‡¿ ðŸ‡¦ðŸ‡¿ ðŸ‡²ðŸ‡³ ðŸ‡ºðŸ‡¦ ðŸ‡°ðŸ‡¿ ðŸ‡°ðŸ‡¬ ðŸ‡¹ðŸ‡¯ ðŸ‡¹ðŸ‡² ðŸ‡ºðŸ‡¿ ðŸ‡¦ðŸ‡¿ ðŸ‡²ðŸ‡³"
	echo ""
end

function footer_banner
	echo ""
	echo "ðŸ‡ºðŸ‡¦ ðŸ‡°ðŸ‡¿ ðŸ‡°ðŸ‡¬ ðŸ‡¹ðŸ‡¯ ðŸ‡¹ðŸ‡² ðŸ‡ºðŸ‡¿ ðŸ‡¦ðŸ‡¿ ðŸ‡²ðŸ‡³ ðŸ‡ºðŸ‡¦ ðŸ‡°ðŸ‡¿ ðŸ‡°ðŸ‡¬ ðŸ‡¹ðŸ‡¯ ðŸ‡¹ðŸ‡² ðŸ‡ºðŸ‡¿ ðŸ‡¦ðŸ‡¿ ðŸ‡²ðŸ‡³ ðŸ‡ºðŸ‡¦"
	echo "ðŸ‡ºðŸ‡¦"
	echo "ðŸ‡ºðŸ‡¦    ðŸ† $argv[1] â€¼ï¸"
	echo "ðŸ‡ºðŸ‡¦"
	echo "ðŸ‡ºðŸ‡¦ ðŸ‡°ðŸ‡¿ ðŸ‡°ðŸ‡¬ ðŸ‡¹ðŸ‡¯ ðŸ‡¹ðŸ‡² ðŸ‡ºðŸ‡¿ ðŸ‡¦ðŸ‡¿ ðŸ‡²ðŸ‡³ ðŸ‡ºðŸ‡¦ ðŸ‡°ðŸ‡¿ ðŸ‡°ðŸ‡¬ ðŸ‡¹ðŸ‡¯ ðŸ‡¹ðŸ‡² ðŸ‡ºðŸ‡¿ ðŸ‡¦ðŸ‡¿ ðŸ‡²ðŸ‡³"
end
```

I had gone some way with this approach before updating these docs, so `common_functions.fish` comes as it is well beyond this step. A consequence of using `common_functions.fish ` is right up the top, `set -U VM_FOLDER /var/www`. It turned that hard coding that made setting all the values based on `VM_FOLDER` as simple as you see there. Doing it by passing an argument from the `Vagrantfile` is sufficiently more complicated to be worth the one item of data duplication. In return I've been able to replace a multitude of strings with those constants.

### Create `provision/scripts/install_apache.fish`

```
#!/bin/fish

# 04 Install Apache (with SSL)

set script_name     "install_apache.fish"
set updated_date    "2024-02-02"

set active_title    "Installing Apache (with SSL ðŸ™ƒ)"
set job_complete    "Apache Installed (with SSL ðŸ™ƒ)"

# Source common functions
source /var/www/provision/scripts/common_functions.fish

header_banner $active_title $script_name $updated_date

# -- -- /%/ -- -- /%/ -- / script header -- /%/ -- -- /%/ -- --

# Arguments...
# NONE!"

# Script variables...

# Always set PACKAGE_LIST when using update_and_install_packages
set PACKAGE_LIST \
	apache2 \
	apache2-bin \
	apache2-data \
	apache2-utils

set -x DEBIAN_FRONTEND noninteractive

# -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- --

# Add repository for ondrej/apache2
LC_ALL=C.UTF-8 apt-add-repository -yu ppa:ondrej/apache2

# Update package lists & install packages
update_and_install_packages $PACKAGE_LIST

announce_success "Apache packages installed successfully!"

# Generate SSL key
echo "ðŸ”„ Generating SSL key ðŸ”„"
if not openssl genrsa \
	-out $PROVISION_SSL/localhost.key \
	2048
	handle_error "Failed to generate SSL key"
end

# Generate self-signed SSL certificate
echo "ðŸ”„ Generating self-signed SSL certificate ðŸ”„"
if not openssl req -x509 -nodes \
	-key $PROVISION_SSL/localhost.key \
	-out $PROVISION_SSL/localhost.cert \
	-days 3650 \
	-subj "/CN=localhost" 2>/dev/null

	handle_error "Failed to generate self-signed SSL certificate"
end

announce_success "SSL files generated successfully!"

# Display information about the generated certificate
openssl x509 -noout -text -in $PROVISION_SSL/localhost.cert

# Copy web server files into place
yes | cp $PROVISION_VHOSTS/local.conf /etc/apache2/sites-available/
yes | cp $PROVISION_SSL/* /etc/apache2/sites-available/
yes | cp $PROVISION_HTML/index.htm $SHARED_HTML/

# Check if index.html exists in the html folder
if not test -e $SHARED_HTML/index.html
	cp $PROVISION_HTML/index.html $SHARED_HTML/
end

# Set permissions on web server files
chmod -R 755 $SHARED_HTML/*
chmod 600 /etc/apache2/sites-available/localhost.key

a2ensite local.conf
a2dissite 000-default
a2enmod rewrite
a2enmod ssl

# Restart Apache to apply changes
systemctl restart apache2

# -- -- /%/ -- -- /%/ -- script footer -- /%/ -- -- /%/ -- --
footer_banner $job_complete
```

By moving common logic to `common_functions.fish`, I've been able to make this script, & all the ones that follow, a lot more concise.


### Create `provision/html/index.htm`

```
<html>
<head>
	<title>Shaka Bom!</title>
</head>
<body>
	<h1>Shaka Bom!</h1>
	<p>How cool is this?</p>
</body>
</html>
```

The page is a simple `index.html` located within your VM in the `/var/www/html` directory, the so-called document root. This document root is the directory that's available from the outside to your server.

### Create `provision/vhosts/local.conf`

```
<VirtualHost *:80>
	ServerAdmin admin@example.com
	DocumentRoot /var/www/html
    DirectoryIndex index.htm index.html index.php

	<Directory /var/www/html>
		# Allow .htaccess rewrite rules
		Options Indexes FollowSymLinks
		AllowOverride All
		Require all granted
	</Directory>

	ErrorLog "/var/log/apache2/localhost-error_log"
	CustomLog "/var/log/apache2/localhost-access_log" common
</VirtualHost>
```

### Update `Vagrantfile`

```
# -*- mode: ruby -*-
# vi: set ft=ruby :

# 04 Install Apache (with SSL)
# Updated: 2024-02-07

# Machine Variables
MEMORY              = 4096
CPUS                = 1
TIMEZONE            = "Australia/Brisbane" # "Europe/London"
VM_IP               = "192.168.22.42"      # 22 = titanium, 42 = Douglas Adams's number

# Synced Folders
HOST_FOLDER         = "."
REMOTE_FOLDER       = "/var/www"

Vagrant.configure("2") do |config|

	config.vm.box = "bento/ubuntu-20.04-arm64"

	config.vm.provider "vmware_desktop" do |v|
		v.memory = MEMORY
		v.cpus   = CPUS
		v.gui    = true
	end

	# Configure network...
	config.vm.network "private_network", ip: VM_IP

	# Set a synced folder...
	config.vm.synced_folder HOST_FOLDER, REMOTE_FOLDER, create: true, nfs: true, mount_options: ["actimeo=2"]

	# Provisioning...
#	config.vm.provision :shell, path: "provision/scripts/upgrade_vm.sh", args: [TIMEZONE]
#	config.vm.provision :shell, path: "provision/scripts/install_utilities.sh", args: [TIMEZONE]
	config.vm.provision :shell, path: "provision/scripts/install_apache.fish"

end
```

Or copy this file...

```
cp ./Vagrantfiles/Vagrantfile_04 ./Vagrantfile
```

### Provision the VM...

If the VM is **not** running

```
vagrant up --provision
```

If the VM is running

```
vagrant reload --provision
```

### Visit

* [http://192.168.22.42/](http://192.168.22.42/)

You should see a minimal page that I put there. For the Apache default page of your VM visit... 

* [http://192.168.22.42/index.html](http://192.168.22.42/index.html)


### All good?

Save the moment with a [Snapshot](./Snapshots.md).

--

| [03 Install Utilities](./03_Install_Utilities.md)
| [**Back to Steps**](../README.md)
| [05 Install PHP (with Composer)](./05_Install_PHP.md)
|
