#!/bin/zsh

# vg.sh
# Updated: 2024-02-12

# Generates Vagrantfile for the step specified by an integer argument.

# Machine Variables
VM_HOSTNAME="p2vagrant"
VM_IP="192.168.22.42"                   # 22 = titanium, 42 = Douglas Adams's number
TIMEZONE="Australia/Brisbane"           # "Europe/London"
MEMORY=4096
CPUS=1

# Synced Folders
HOST_FOLDER="."
REMOTE_FOLDER="/var/www"

# Software Versions
PHP_VERSION="8.3"
MYSQL_VERSION="8.3"
SWIFT_VERSION="5.9.2"                   # For installing Swift (optional)

# Database Variables
ROOT_PASSWORD="RootPassw0rd"
DB_USERNAME="fredspotty"
DB_PASSWORD="Passw0rd"
DB_NAME="example_db"
DB_NAME_TEST="example_db_test"

# Vagrantfile data...
data_file="./provision/data/vagrant_data"

# Argument...
vm_step=$1

# Script variables...
step_title=""
file_lines=()

# Sparse array of provisioning script calls, indexed by setup step...
provisioning_indexes=(2 3 5 6 7 8 10)
provisioning_items[2]='\tconfig.vm.provision :shell, path: "provision/scripts/upgrade_vm.sh", args: [TIMEZONE]'
provisioning_items[3]='\tconfig.vm.provision :shell, path: "provision/scripts/install_utilities.sh"'
provisioning_items[5]='\tconfig.vm.provision :shell, path: "provision/scripts/install_swift.fish", args: [SWIFT_VERSION]'
provisioning_items[6]='\tconfig.vm.provision :shell, path: "provision/scripts/install_apache.fish", args: [VM_HOSTNAME, VM_IP]'
provisioning_items[7]='\tconfig.vm.provision :shell, path: "provision/scripts/install_php.fish", args: [PHP_VERSION]'
provisioning_items[8]='\tconfig.vm.provision :shell, path: "provision/scripts/install_mysql.fish", args: [MYSQL_VERSION, PHP_VERSION, ROOT_PASSWORD, DB_USERNAME, DB_PASSWORD, DB_NAME, DB_NAME_TEST]'
provisioning_items[10]='\tconfig.vm.provision :shell, path: "provision/scripts/configure_sites.fish"'

# -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- #

# Function for error handling
# Usage: handle_error "Error message"
handle_error() {
	echo "‚ö†Ô∏è Error: $1 üí•"
	exit 1
}

# Function to announce success
# Usage: announce_success "Task completed successfully." [use_alternate_icon]
announce_success() {
	icon="‚úÖ"

	if [ -n "$2" ] && [ "$2" -eq 1 ]; then
		icon="üëç"
	fi

	echo "$icon $1"
}

# -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- #

# Function to read and parse the data file
# Usage: read_and_parse_data $vm_step $data_file
read_and_parse_data() {
	local vm_step=$1
	local file_temp=$2

	while IFS= read -r one_line; do
		# Skip lines starting with #
		[[ "$one_line" =~ ^# ]] && continue

		split_line=(${(s:|:)one_line})  # Split the line by |

		if [[ "${split_line[1]}" -eq "$vm_step" ]]; then
			step_title="${split_line[2]}"
			announce_success "Generating Vagrantfile for $vm_step: $step_title."
			return 0  # Valid step found in the data file
		fi
	done < "$file_temp"

	handle_error "Step not found in the data file"
}

# Function to set the Vagrantfile header
# Usage: set_header_lines $vm_step $step_title
function set_header_lines () {
	local i=$(printf "%02d" $1)
	local title=$2

	# Add the Vagrantfile header to the file_lines array
	file_lines+=( "# -*- mode: ruby -*-" )
	file_lines+=( "# vi: set ft=ruby" )
	file_lines+=( "" )
	file_lines+=( "# $i $title" )
	file_lines+=( "# Generated: $(date "+%Y-%m-%d")" )
	file_lines+=( "" )
	file_lines+=( "# Machine Variables" )
	file_lines+=( "VM_HOSTNAME         = \"$VM_HOSTNAME\"" )
	file_lines+=( "VM_IP               = \"$VM_IP\"" )
	file_lines+=( "TIMEZONE            = \"$TIMEZONE\"" )
	file_lines+=( "MEMORY              = $MEMORY" )
	file_lines+=( "CPUS                = $CPUS" )
}

# Function to set the Vagrantfile variable lines
# Usage: set_variables_lines $vm_step
function set_variables_lines () {
	local vm_step=$1

	file_lines+=( "" )
	file_lines+=( "# Synced Folders" )
	file_lines+=( "HOST_FOLDER         = \"$HOST_FOLDER\"" )
	file_lines+=( "REMOTE_FOLDER       = \"$REMOTE_FOLDER\"" )

	if (( vm_step >= 5 )); then
		file_lines+=( "" )
		file_lines+=( "# Software Versions" )
		file_lines+=( "SWIFT_VERSION       = \"$SWIFT_VERSION\"" )
	fi
	if (( vm_step >= 7 )); then
		file_lines+=( "PHP_VERSION         = \"$PHP_VERSION\"" )
	fi
	if (( vm_step >= 8 )); then
		file_lines+=( "MYSQL_VERSION       = \"$MYSQL_VERSION\"" )
	fi
	if (( vm_step >= 7 )); then
		file_lines+=( "" )
		file_lines+=( "# Database Variables" )
		file_lines+=( "ROOT_PASSWORD       = \"$ROOT_PASSWORD\"" )
		file_lines+=( "DB_USERNAME         = \"$DB_USERNAME\"" )
		file_lines+=( "DB_PASSWORD         = \"$DB_PASSWORD\"" )
		file_lines+=( "DB_NAME             = \"$DB_NAME\"" )
		file_lines+=( "DB_NAME_TEST        = \"$DB_NAME_TEST\"" )
	fi
}

# Function to set VM config opening lines
# Usage: set_config_opening_lines
function set_config_opening_lines () {
	file_lines+=( "" )
	file_lines+=( 'Vagrant.configure("2") do |config|' )
	file_lines+=( "" )
	file_lines+=( "\tconfig.vm.box = \"bento/ubuntu-20.04-arm64\"" )
	file_lines+=( "" )
	file_lines+=( "\tconfig.vm.provider \"vmware_desktop\" do |v|" )
	file_lines+=( "\t\tv.memory    = MEMORY" )
	file_lines+=( "\t\tv.cpus      = CPUS" )
	file_lines+=( "\t\tv.gui       = true" )
	file_lines+=( "\tend" )
	file_lines+=( "" )
	file_lines+=( "\t# Configure network..." )
	file_lines+=( "\tconfig.vm.network \"private_network\", ip: VM_IP" )
	file_lines+=( "" )
	file_lines+=( "\t# Set a synced folder..." )
	file_lines+=( "\tconfig.vm.synced_folder HOST_FOLDER, REMOTE_FOLDER, create: true, nfs: true, mount_options: [\"actimeo=2\"]" )
}

# Function to set VM config provisioning lines
# Usage: set_provisioning_lines $vm_step
function set_provisioning_lines () {
	local vm_step=$1

	if (( vm_step >= 4 )); then
		file_lines+=( "" )
		file_lines+=( "\t# Upgrade check..." )
		file_lines+=( '\tconfig.vm.provision :shell, path: "provision/scripts/upgrade_vm.fish", args: [VM_HOSTNAME], run: "always"' )
	fi

	if (( vm_step >= 2 )); then
		file_lines+=( "" )
		file_lines+=( "\t# Provisioning..." )

		# Loop through keys of the associative array in sorted order
		for prov_step in "${provisioning_indexes[@]}"; do
			prov_string=$provisioning_items[$prov_step]
			if (( $vm_step <= $prov_step )); then
				if (( $vm_step == $prov_step )); then
					file_lines+="$prov_string"
				fi
				return
			fi
			file_lines+=( "#$prov_string" )
		done
	fi
}

# Function to set VM config closing lines
# Usage: set_config_closing_lines
function set_config_closing_lines () {
	file_lines+=( "" )
	file_lines+=( "end" )
}

# -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- /%/ -- -- #

read_and_parse_data $vm_step $data_file

set_header_lines $vm_step $step_title
set_variables_lines $vm_step
set_config_opening_lines
set_provisioning_lines $vm_step
set_config_closing_lines

# Write file_lines to a file (e.g., Vagrantfile_test) with line breaks and actual tabs
printf "%s\n" "${file_lines[@]}" | sed 's/\\t/'$'\t'/g > ./Vagrantfile
